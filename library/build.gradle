apply plugin: 'com.android.library'
apply plugin: 'maven-publish'

ext {
    PUBLISH_GROUP_ID = 'com.abdeveloper'
    PUBLISH_ARTIFACT_ID = 'multi-select-dialog-v21'
    PUBLISH_VERSION = 'zqy_mod_1.0.0'
}

android {
    compileSdk 34
    namespace "com.abdeveloper.library_v21"

    defaultConfig {
        minSdkVersion 21
        targetSdkVersion 34
        multiDexEnabled true
        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"

    }
    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }

    // https://stackoverflow.com/questions/71365373/software-components-will-not-be-created-automatically-for-maven-publishing-from
    publishing {
        singleVariant('release') {
            withSourcesJar()
            withJavadocJar()
        }
    }
}

dependencies {
    // https://stackoverflow.com/questions/75263047/duplicate-class-in-kotlin-android
    // https://stackoverflow.com/a/75719642
    implementation(platform("org.jetbrains.kotlin:kotlin-bom:1.8.22"))

    implementation fileTree(dir: 'libs', include: ['*.jar'])
    implementation 'androidx.appcompat:appcompat:1.7.0'
    implementation 'androidx.recyclerview:recyclerview:1.3.2'
}

afterEvaluate {
    publishing {
        publications {
            release(MavenPublication) {
                from components.getByName('release')
                groupId PUBLISH_GROUP_ID
                artifactId PUBLISH_ARTIFACT_ID
                version PUBLISH_VERSION
            }
        }
        repositories { maven { url "./../local-maven-repository" } }
    }
}